;<SCHULZ>SYSSAV.MAC;2    23-NOV-76 15:28:47    EDIT BY SWEER
;MADE $RDBT INTERNAL ALSO AND ADDED MISSING IMULI 5,NSECPG
;<MON>SYSSAV.SMX		7/75			EDIT BY HEATHMAN
;MADE $WRTBT AND $SUMBT EXTERN SO WE CAN REMOVE LOCAL SYMBOLS 
;<MON>SYSSAV.SMX;		14-FEB-75		EDIT BY HEATHMAN
;RE ORGANIZED DISK SPACE ALLOCATION PARAMETERS

	SEARCH STENEX,PROLOG
	.TITLE SYSSAV

	EXTERN NSECPG,RESERV,ENTFLG,SVNUM
REPEAT 0,<		;!! MAH @ SUMEX 2/75 !! NOW DEFINED IN DSKPAK
RESAR==170		;NO. OF PAGES USED FOR RESIDENT MONITOR
NAREA==4		;ALSO DEFINED IN DSKPAK
>
	EXTERN NSYSSV,DSKVML,RESAR	;LINK FROM DSKPAK
	EXTERN VERSIZ,GETSMF,DBUGSW
	EXTERN VERCEL,SWPCOR,SWCEND

	INTERN $WRTBT,$RDBT,$SUMBT
;
;
;
;
;   PARAMETERS

DATA=600000	;DATA BUFFER TO READ IN CONTROL PAGE
;;;VERSNL==<NPACKS*^D406-^D16>*^D200	;STARTING ADDRESS
ARSZ=5		;SIZE OF DESCRIPTION OF EACH AREA IN CONTROL PAGE

PATRN:	132333435363
	ASCII " NOT "
	ASCII "USED "
   REPEAT ARSZ-2,<0>


LOC:	     	;VALUES TO INITIALIZE FOR OPERATOR
	RESERV
	DBUGSW
	GETSMF
	BLOCK 5	;ALLOW FOR PATCHES
CELLSZ==.-LOC
VAL:	
	1
	0	;DBUGSW
	2	;GETSMF
	BLOCK 5

;;;BOOTPG:	VERSNL+1B0	;AREA FOR SYSTEM VERSIONS--LINEAR ADDRESS
				;!! MAH @ SUMEX 2/75 !! DEFINED NOW IN DSKPAK
				;AS DSKVML

WRITOP:	1000+1B14		;WRITE 1000 PAGES

FORMAT:	ASCII " 00  "	;FORMAT WORD TO BE ADDED TO VERS NO.

TIMFLG:	XWD	045200,0	;PARAM TO ODTIM

FRMMSK:	BYTE   (7)177,170,170,177,177   ;MASK OFF VERSION

IN1:	XWD	PATRN,DATA

IN2:	XWD	DATA+1,DATA+ARSZ+1

AREASV:	0		;SAVE CELL FOR AREA NO.

ENTR:	0		;FLAG INDICATING WHAT ENTRY POINT WAS USED

HISAV:	0		;TEMPORARY SAVE OF SWPMON DSKADR

$SUMBT:
	SETOM ENTR
	JSP 16,$RDPG	;READ CONTROL PAGE
;
	JSP 16,$BTSUM	;PRINT SUMMARY OF WHAT IS IN SYS AREAS
	JRST $BOOT

$BTER:
	HRROI 1,[ASCIZ /
IO ERROR IN CONTROL PAGE/]
	PSOUT
	JRST $IBOOT	;INITIALIZE CONTROL PAGE

$BT1:
	HRROI 1,[ASCIZ /
CONTROL PAGE NOT INITIALIZED/]
	PSOUT
$IBOOT:
	HRROI 1,[ASCIZ /
REFORMAT?/]
	PSOUT
	PBIN	;WAIT FOR REPLY--"Y" IS ONLY VALID ONE
	CAIE 1,"Y"
	JRST $BOOT	;DON'T REFORMAT
	HRROI 1,[ASCIZ / [CONFIRM]/]
	PSOUT
	PBIN		;CONFIRM IN CASE TYPING ERROR--C/R ONLY VALID
			;REPLY
	CAIE 1,37
	JRST $BOOT

	MOVE 2,IN1		;BLT INITIALIZATION DATA INTO PAGE
	BLT 2,DATA+ARSZ
	MOVE 2,IN2
	BLT 2,DATA+777

	JSP 16,$WRTPG
	JRST $BOOT
;
;
$RDBT:	SETZM ENTR		;READ BOOT AREA
	MOVE 1,[XWD $RBBG,601000]
	BLT 1,601000+$RBBLT	;MOVE UP READ ROUTINE
	JRST $BOOT
;
;
$WRTBT:
	MOVEI 1,1
	MOVEM 1,ENTR		;WRITE A SYSTEM OUT
;
;
$BOOT:
	SKIPGE ENTR	;DISPLAY ONLY
	HALTF		;YES-EXIT
	HRROI 1,[ASCIZ /
AREA: /]				;ASK FOR DESIRED AREA
	PSOUT
	MOVEI 1,100
	MOVEI 3,12		;RADUIS DECIMAL
	NIN
	JRST $BT3	;ERROR
	JUMPE 2,$BT3
	CAILE 2,NSYSSV		;CHECK VALIDITY
	JRST $BT3		;MUST BE 1-NSYSSV
	SUBI 2,1
	MOVEM 2,AREASV	;SAVE AREA NUMBER
	HRROI 1,[ASCIZ / [CONFIRM]/]
	PSOUT			;ALLOW FOR TYPING ERRORS
	PBIN
	CAIE 1,37
	JRST $BOOT
;
	JSP 16,$RDPG	;READ CONTROL PAGE TO RECORD WHERE SYS IS
;
	SKIPN ENTR	;WANT TO EAD SYSTEM?
	JRST $RBT1	;YES-GO D IT
;
;
;	SET UP STANDARD VALUES FOR OPERATORS
	MOVSI 2,-CELLSZ
$BT4:
	MOVE 3,VAL(2)
	SKIPE LOC(2)

	MOVEM 3,@LOC(2)
	AOBJN 2,$BT4
;
	MOVE 1,AREASV
	IMULI 1,VERSIZ	;COMPUT LINEAR ADDRESS OF AREA
	IMULI 1,NSECPG
	ADD 1,DSKVML
	ADDI 1,NSECPG
	MOVEI 5,RESAR
	IMULI 5,NSECPG
	ADD 5,1		;CALCULATE SWPMON ADDRESS
	MOVEM 5,HISAV
	MOVEM 5,VERCEL	;SAVE ADDRESS FOR SYSTEM
;
	MOVEI 5,0		;WRITE OUT RESIDENT MONITOR
	MOVE 4,SWPCOR		;ENDING PAGE NO. OF RES MON
	JSP 16,$BTIO
;
	MOVE 1,HISAV	;GET SWPMON ADDRESS
	MOVEI 5,SWPMP0	;START OF SWAPPABLE MONITOR
	MOVE 4,SWCEND		;ENDING PAGE OF SWAP MON
	ADDI 4,1
	JSP 16,$BTIO
;
;
	MOVE 15,AREASV
	IMULI 15,ARSZ		;INDEX INTO CONTROL PAGE
	ADDI 15,1
;
	SETZM 2
	SETZM DATA(15)		;CLEAR DESCRIPTTION
	MOVEI 3,DATA+1(15)
	HRLI 3,DATA(15)
	BLT 3,DATA-1+ARSZ(15)
	MOVE 2,SVNUM
	HRROI 1,DATA(15)
	MOVEI 3,12
	NOUT
	JFCL
;
	MOVE 4,SWPCOR		;SAVE SIZES OF PARTS OF MON FOR REBOOT
	HRL 4,SWCEND
	MOVEM 4,DATA+ARSZ-1(15)
	MOVEI 1,DATA(15)	;PUT TIME AND DATE OF WRITE
	HRLI 1,(<POINT 7,0,34>)
	SETOM 2			;USE CURRENT DATE
	MOVE 3,TIMFLG		;FLAGS
	ODTIM
	JSP 16,$WRTPG		;WRITE OUT CONTROL PAGE
;
	HRROI 1,DATA(15)	;WRITE OUT VERSION
	PSOUT
	HRROI 1,[ASCIZ / WRITTEN ON AREA /]
	PSOUT
	MOVEI 1,101
	MOVE 2,AREASV	;WRITE OUT AREA NUMBER
	ADDI 2,1
	MOVEI 3,12
	NOUT
	JFCL
	HRROI 1,[ASCIZ /
/]
	PSOUT
	HALTF
	JRST .-1
;
;

$WRTPG:	MOVE 2,WRITOP		;WRITE CONTROL PAGE
	SKIPA
$RDPG:	MOVEI 2,1000		;READ CONTROL PAGE
	MOVE 1,DSKVML		;LINEAR ADDRESS
	MOVEI 3,DATA
	DSKOP
	JUMPN 1,$BTER		;I/O ERROR
	MOVE 1,DATA	;INITIALIZED YET
	CAME 1,PATRN
	JRST $BT1	;NO
	JRST 0(16)

$BTSUM:			;PRINT OUT WHAT IS IN CONTROL PAGE
;
	HRROI 1,[ASCIZ /
AREA  STATUS
/]
	PSOUT
	MOVNI 4,NSYSSV	;LOADER WON'T LINK NEGATIVE #'S IN LEFT HALF
	HRLZ 4,4
;;;	MOVSI 4,-NSYSSV
$BTSM1:
	HRROI 1,[ASCIZ /
/]
	PSOUT
;
	MOVEI 1,101
	MOVEI 2,0(4)	;PRINT OUT AREA NUMBER
	ADDI 2,1
	MOVEI 3,12
	NOUT
	JFCL
	HRROI 1,[ASCIZ /     /]
	PSOUT
;
	MOVEI 1,0(4)
	IMULI 1,ARSZ
	ADD 1,[XWD -1,DATA+1]	;STRING POINTER FOR AN AREA
	PSOUT
	AOBJN 4,$BTSM1
	JRST (16)

$BT3:
	HRROI 1,[ASCIZ /INVALID AREA/]
	PSOUT
	JRST $BOOT

$BTIO:
	MOVE 3,5
	LSH 3,^D9		;MAKE PAGE ADDRESS
	MOVE 2,WRITOP
	JUMPN 3,$BTIO2
	MOVEI 3,20
	SUBI 2,20
$BTIO2:
	MOVE 6,1		;SAVE LINEAR ADR
	DSKOP
	EXCH 6,1
	JUMPN 6,$BTIO1		;I/O ERROR
	ADDI 1,NSECPG
	ADDI 5,1
	CAMGE 5,4
	JRST $BTIO		;MORE TO WRITE
	JRST (16)
$BTIO1:
	HRROI 1,[ASCIZ /
IO ERROR IN MONITOR PAGE/]
	PSOUT
	JRST $BOOT
;
;
;

$RBBG:
	PHASE 601000
$RBT1:		;READ IN MONITOR
	MOVE 15,AREASV
	IMULI 15,ARSZ	;INDEX INTO CONTROL PAGE
	ADDI 15,1

	MOVE 1,AREASV
	IMULI 1,VERSIZ
	IMULI 1,NSECPG
	ADD 1,DSKVML		;LINEAR ADR OF SAVED SYSTEM
	ADDI 1,NSECPG
	MOVEI 5,RESAR
	IMULI 5,NSECPG
	ADD 5,1
	MOVEM 5,$TEMP
	HRRZ 4,DATA+ARSZ-1(15)	;ENDING PAGE
	MOVEI 5,0		;STARTING PAGE
	JSP 16,$RBTIO  ;SEPARATE ROUTINE SO IT WONT GET READ OVER

	MOVE 1,$TEMP
	MOVEI 5,SWPMP0
	HLRZ 4,DATA+ARSZ-1(15)
	ADDI 4,1
	JSP 16,$RBTIO


	HRROI 1,[ASCIZ /READING COMPLETE
/]
	PSOUT
	HALTF
$TEMP:	0
;
;
$RBTIO:
	MOVEI 2,1000	;SIZE
	MOVE 3,5
	LSH 3,^D9	;PAGE ADR
	MOVE 6,1	;SAVE LINEAR ADR
	JUMPN 3,$RBIO1
	MOVEI 3,20
	SUBI 2,20	;SPECIAL FOR PAGE 0
$RBIO1:
	DSKOP
	EXCH 6,1
	JUMPN 6,$RBTO1
$RBTO2:
	ADDI 1,NSECPG
	ADDI 5,1
	CAMGE 5,4
	JRST $RBTIO
	JRST (16)

$RBTO1:
	EXCH 6,1
	HRROI 1,[ASCIZ /
IO ERROR READING MONITOR-CONTINUING/]

	PSOUT
	EXCH 6,1
	JRST $RBTO2
;
$RBT2:
	HRROI 1,[ASCIZ / AREA NOT IN USE
/]
	PSOUT
	JRST $BOOT
;
	DEPHASE
$RBBLT==.-$RBBG
	.END
