;<NEWMON>SSYS.MAC;5    26-AUG-74 15:59:51	EDIT BY MANN     
	SEARCH	PROLOG,FILEDEF,STENEX
	.TITLE	SSYS

;SUMEX-AIM  JSYSS

	INTERN .PRGE,.VKEEP

	EXTERN MENTR,MRETN,CAPENB,BUGCHK

	EXTERN ERRD,ERUNLD
	EXTERN ACCCHK,CHKJFN,DELDEL,GETFDB,MDDNAM,RELJFN,UNLCKF,USTDIR
  

DEFINE	ERUNLK(ERRORN,EXTRA)<
 JRST [	EXTRA
	IFDIF  <ERRORN>,<>,<MOVEI A,ERRORN>
	JRST	ERUNLD]>

DEFINE	ERR(ERRORN,EXTRA)<
 JRST [	EXTRA
	IFDIF  <ERRORN>,<>,<MOVEI A,ERRORN>
	JRST	ERRD]>
;PURGE  DELETE AND EXPUNGE FILES

;ACCEPTS:
;	  1)  JFN
;
;RETURNS:
;	 +1) ERRORS IN 1
;	 +2) IF SUCCESSFUL THE JFN IS RELEASED 

.PRGE:
	JSYS	MENTR
	HRRZ	JFN,1		;SAVE JFN
	PUSHJ	P,CHKJFN
	ERR 	()
	JFCL			;STRING
	ERUNLK	DESX4		;TTY BOTH BAD
	HRRZ	A,NLUKD(DEV)	       
	CAIE	A,MDDNAM
	ERUNLK	GFDBX1
	PUSHJ	P,GETFDB	;ADD OF FDB TO A
	ERUNLK	DESX3
	PUSHJ	P,CWACCS	;NEED WRITE ACCES TO PURGE
	
	MOVSI	D,FDBDEL!FDBSDL	;MARK FOR DELETE AND SEL EXPUNGE
	IORM	D,FDBCTL(A)

	PUSHJ	P,USTDIR	;UNLOCK THE DIRECTORY
	PUSH	P,JFN
	HRRZ	JFN,FILDDN(JFN)	;GET DIRECTORY NUMBER
	SETO	16,
	PUSHJ	P,DELDEL	;EXPUNGE ALL FILE MARK FOR IT
	POP	P,JFN

	UMOVE	A,1
	PUSHJ	P,RELJFN	;YES
	JRST	SKKKRT
PURGE1:	PUSHJ	P,UNLCKF	


SKKKRT:	AOS	0(P)	
	JRST	MRETN

CWACCS:	PUSH	P,A	;A HAS  ADDR OF FDB
	PUSH	P,B
	HRLI	A,WRTF	;CHECK WRITE ACCESS
	PUSHJ	P,ACCCHK
	JRST	NWAEER
	POP	P,B
	POP	P,A
	POPJ	P,0
NWAEER:	SUB	P,[XWD 3,3]
	PUSHJ	P,USTDIR
	PUSHJ	P,UNLCKF
	ERR	DELFX1


;VERSIONS OF A FILE TO KEEP
;ACCEPTS:
;	  1)  JFN   SHOULD BE THE JFN OF THE HIGHEST VERSION NUMBER
;     	  2)  B30 OFF  DELETE AND EXPUNGE EXTRA VERSIONS OF THE FILE
;	      B30 ON   DELETE EXTRA VERSIONS OF THE FILE
;	      B31 - B35  NUMBER OF VERSIONS TO KEEP
;
;	VKEEP

;RETURNS:
; 	 +1)  ERRORS IN 1
;	 +2)  IF SUCCESSFUL NO CHANGE TO ACS

.VKEEP:
	JSYS	MENTR
	HRRZ	JFN,1
	PUSHJ	P,CHKJFN	;CHECK OUT JFN
	ERR	()
	JFCL
	ERUNLK	DESX4
	HRRZ	A,NLUKD(DEV)
	CAIE	A,MDDNAM
	ERUNLK	GFDBX1
	PUSHJ	P,GETFDB
	ERUNLK	DESX4

	UMOVE	B,2		;GET NUMBER OF VERSION TO KEEP
	SKIPGE	C,B		;NEGATIVE NUMBER
	MOVN	B,B		;YES NEGATE
	CAILE	B,37
	MOVEI	B,37
	SKIPGE	C
	TRO	B,40
	CAIA
VKEEP1:
	ADDI	A,DIRORG		;MAKE DIR ADDR OF THE FDB 
	PUSHJ	P,CWACCS	;check for write access first
	DPB	B,[POINT  6,FDBBYV(A),5]	;SET THE BITS IN FDB
	HRRZ	A,FDBVER(A)		;GET NEXT VERSION
	JUMPN	A,VKEEP1		;IF NOT DONE
	
	PUSHJ	P,USTDIR
	PUSHJ	P,UNLCKF
	JRST	SKKKRT
	.END


