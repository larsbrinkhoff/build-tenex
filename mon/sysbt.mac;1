	SEARCH	PROLOG
	.TITLE	SYSBT	system bootstrap handler

;ENTRY POINT FROM PAPER TAPE BOOT TO QUERY OPERATOR FOR START UP OPTIONS
;OPTIONS ARE: EDDT, SYSGO1 WITH EDDT LOADED AND BREAKPOINTS INSERTED, AND
;SYSGO1 W/O EDDT OR BREAKPOINTS

	EXTERN	DDT,SAVPI,AUTOPI,B1ADR,BP1,B1INS ;linkage to EDDT
	EXTERN	BUGMSG,BUGTYO			;linkage to TTYSRV
	EXTERN	BUGHLT,BUGADR,BUGCHK,CHKADR,DUMPSW,SYSGO1 ;linkage to PISRV
	EXTERN	DBUGSW,DCHKSW
	INTERN	SYSBT,SYSBTW,SYSBTN,SYSBTD	;linkage to PISRV, operator

	LOC RESMA0-1000		;PUT THIS OUT OF THE WAY, JUST BELOW RESMA0

SYSBT:	PIOFF 610000		;RESET THE PI SYSTEM
	CONO APR,200000		;RESET THE APR

	SETZM 105		;BURN OUR BRIDGES (SINCE WERE GONNA GET 
				;CLOBBERED)
REPEAT 0,<
	MOVEI 1,[SIXBIT '$DO YOU KNOW WHAT YOU ARE DOING? /']
	JSR BUGMSG		;SEE IF KNOWLEDGEABLE PERSON AROUND

	JSR BUGRDB		;GET A CHARACTER

	JSR BUGTYO		;ECHO CHARACTER
	CAIE 1,"Y"
>
	SKIPE 1
	 JRST SYSBTW		;DOESN'T KNOW WHAT HE'S DOING, GIVE 
				;REASONABLE DEFAULTS (START WITH EDDT LOADED)

SYSB0:	MOVEI 1,[SIXBIT '$E (EDDT), W (START WITH EDDT LOADED), $N (START WITH NO EDDT), OR D (DEBUGGING START)? /']
	JSR BUGMSG

	JSR BUGRDB		;FIND OUT WHAT HE WANTS
	JSR BUGTYO		;ECHO CHAR

	CAIN 1,"E"		;EDDT?
	 JRST DDT		;YES, DO IT NOW
	CAIN 1,"W"		;START WITH EDDT LOADED?
	 JRST SYSBTW		;YES
	CAIN 1,"N"		;START WITH NO EDDT?
	 JRST SYSBTN		;YES
	CAIN 1,"D"		;DEBUGGING ENTRY?
	 JRST SYSBTD		;YES

	MOVEI 1,[SIXBIT ' ?? DOES NOT COMPUTE/'] 
	JSR BUGMSG
	JRST SYSB0
	SUBTTL	SYSTEM START UP ROUTINES

SYSBTD:	MOVEI 1,2		;DEBUGGING ENTRY
	CAIA

SYSBTW:	MOVEI 1,1		;start up with EDDT loaded, DBUGSW=1
	MOVEM 1,DBUGSW
	SETOM DCHKSW		;DCHKSW=-1
	SETOM DUMPSW		;DUMPSW=-1

	SETZM B1ADR		;do a $B and clear all break points
	MOVE 1,[XWD B1ADR,B1ADR+1]
	BLT 1,AUTOPI

	MOVE 1,[XWD BUGCHK,CHKADR] ;set up CHKADR(BUGCHK)$7B
	MOVEI 2,7
	JSR SETBP

	MOVE 1,[XWD BUGHLT,BUGADR] ;set up BUGADR(BUGHLT)$8B
	MOVEI 2,^D8
	JSR SETBP

	JRST SYSB3		;go start up system

SYSBTN:	SETZM DBUGSW		;start system w/o EDDT, set DBUGSW=0
	SETZM DCHKSW		;DCHKSW=0
	SETOM DUMPSW		;DUMPSW=-1

SYSB3:	MOVEI 1,[SIXBIT '$/']	;GIVE CRLF
	JSR BUGMSG

	JRST SYSGO1		;go start system
	SUBTTL	utility subroutines

SETBP:	0
	SOJ 2,			;turn BP # into offset
	IMULI 2,3

	MOVEM 1,B1ADR(2)	;put it into BP table

	MOVEI 3,BP1(2)		;insert BP into core
	HRLI 3,(<JSR 0>)
	EXCH 3,(1)
	MOVEM 3,B1INS(2)

	JRST @SETBP	      	;return

BUGRDB:	0
	CONSO TTY,1B30		;read in a byte from console tty, wait for
	JRST .-1		;input

	DATAI TTY,1		;input byte
	ANDI 1,137		;make upper case ASCII
	JRST @BUGRDB

	.END
