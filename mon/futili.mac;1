;<DLM/TEMP>FUTILI.MAC;10    25-OCT-72  9:03:32	EDIT BY TOMLINSON
;<FILESYSTEM>FUTILI.MAC;9    25-AUG-72 17:27:35	EDIT BY TOMLINSON
;<FILESYSTEM>FUTILI.MAC;8    29-JUN-72  9:55:23	EDIT BY TOMLINSON

	SEARCH	FILEDEF,STENEX
	TITLE	FUTILITY
	SUBTTL	R.S.TOMLINSON 8 JUN 72

; Pointers to things in fdb

	USE	RESPC

PFILPC::POINT 18,FDBBYV(A),35	; Pointer to size in pages of file
PFILBS::POINT 6,FDBBYV(A),11	; Pointer to last byte size
PFILVC::POINT 6,FDBBYV(A),5	; Pointer to version retention count
PBYTPO::POINT 6,FILBYT(JFN),5	; Points to "p" of file byte pointer
PBYTSZ::POINT 6,FILBYT(JFN),11	; Points to "s" of file byte pointer

	USE	SWAPPC

EXTERN	SKPRET,MAXLC,MAXLW,ASGFRE,JSBFRE,BHC,ASGJFR

; Save ac's before monitor call

	USE	SWAPPC

SAVAC::	ADD P,BHC+NSAC-1	; Make room for nsac ac's
	JUMPGE P,MSTKOV
	PUSH P,-<NSAC-1>(P)		; Move return to end
	MOVEM NSAC-1,-1(P)		; Save last ac
	MOVEI NSAC-1,-NSAC(P)		; Make blt pointer
	BLT NSAC-1,-2(P)		; Blt onto stack
	POPJ P,

RESAC::	MOVSI NSAC-1,-NSAC(P)		; Blt from stack to 0
	BLT NSAC-1,NSAC-1
	POP P,-NSAC(P)			; Move return to new top
	SUB P,BHC+NSAC-1	; Flush the room on the stack
	POPJ P,

; Copy string from user to a free area in the jsb
; Call:	A	; String pointer
;	PUSHJ P,CPYFUS
; Return
;	+1	; No room
;	+2	; Ok
;	A	; Lookup pointer

CPYFUS::PUSH P,A
	MOVEI A,JSBFRE
	MOVEI B,MAXLW+1
	NOINT
;;;	PUSHJ P,ASGFRE
	PUSHJ P,ASGJFR		; CALL ASGJFR   *** 7/5/73 RS ****
	JRST [	POP P,A
		POPJ P,]
	POP P,B
CPYFU1::MOVEI C,MAXLC		; Enter here when storage isnt needed
	PUSH P,A
	EXCH A,B
	HRLI B,(<POINT 7,0,35>)
	JUMPGE A,CPYUS1
	CAML A,[777777000000]
	HRLI A,(<POINT 7,0>)
CPYUS1:	XCTBU [ILDB D,A]
	SOSG C
	MOVEI D,0		; After maxlc chars, force null
	JUMPE D,CPYUS2
	CAIL D,140		; LOWER CASE?
	TRZ D,40		; YES, RAISE
	IDPB D,B
	JRST CPYUS1

CPYUS2:	MOVE A,(P)
	MOVEM B,(P)
	IDPB D,B
	HRL B,A			; Location to lh of a
	SUB A,B			; Compute number of full words
	HRRI B,1(A)
	MOVS A,B		; Is now a lookup pointer
	POP P,B
	JRST SKPRET

; Copy string to user
; Call:	A	; User pointer
;	B	; Locatin of string block
;	PUSHJ P,CPYTUS

CPYTUS::JUMPGE A,STDIR0
	CAML A,[777777000000]
	HRLI A,(<POINT 7,0>)
STDIR0:	HRLI B,(<POINT 7,0,35>)
STDIR2:	ILDB C,B
	JUMPE C,STDIR3
	XCTBU [IDPB C,A]
	JRST STDIR2

STDIR3:	UMOVEM A,2
	XCTBU [IDPB C,A]
	POPJ P,

END
