	SEARCH STENEX
AAA=BIOMRA_^D9
;    THIS  PROGRAM IS LOADED AT 0 AND THEN IS BLT'D UP
;	AT RUN TIME TO A LOCATION WHERE THE BIOM STARTS.
;	THIS IS SO IT WILL BE OUT OF THE WAY OF VALID DUMP DATA
START:
	MOVE 1,[XWD PROST,AAA]
	BLT 1,AAA+PROSZ+2000  ;BLT UP PROGRAM AND SYMBOL TABLE
	MOVE 1,116	;UPDATE SYMBOL TABLE POINTER
	ADDI 1,AAA
	MOVEM 1,116
	MOVEM 1,SAV116
	JRST BGN1

	LIT
;
;
PROST=.	;DON'T MOVE THIS STARTING SYMBOL
	PHASE AAA
STKSZ=20	;PUSH DOWN STACK SIZE
P=17	;STACK REGISTER

TADSEC=3	;OFFSET IN FIXED AREA OF TIME
TADDAY=2
TODCLK=4

CRADAT=26	;POINTER IN CRASH DUMP TO FIXED CONSTANTS
WHEEL=400000

CTLBUF:	BLOCK CRABSZ   ;AREA IN WHICH TO READ CONTROL PAGE
;	PUT FIRST TO BE ON PGE BOUNDARY
JOBNO:	0

ERFLG:	0

SAV116:	BLOCK 1

STK:	BLOCK STKSZ   ;PDL STACK


DATSTR:	BLOCK 5	;AREA WHERE DATE/TIME STRING IS SAVED

FILSTR:	BLOCK 30	;FILE-NAME STRING

DSTR:	ASCIZ  "<CRASHDUMP>"

;
;
;	THIS PROGRAM IS RUN BY JOB0 TO COPY A CRASH DUMP
;	FROM THE FIXED DUMP AREA TO A FILE IN THE
;	<DUMP> DIRECTORY.
;
;	CAN ALSO BE INVOKED FROM A TERMINAL TO INITIALIZE THE
;	DUMP AREA TO INDICATE "EMPTY"
;
BGN1:
	MOVE P,[IOWD STKSZ,STK]	;STACK POINTER
	SETZM ERFLG		;INITIALIZATION
	GJINF		;GET JOB CHARACTERISTICS
	MOVEM 3,JOBNO	;SAVE JOB0 FLAG
	SKIPE JOBNO	;STARTED BY JOB0
	JRST DMP1	;NO--ASK WHAT TO DO
STRT1:	PUSHJ P,PROCDP	;PROCESS ENTIRE DUMP AREA
;
XIT:
	MOVE 1,SAV116	;RESTORE SYMBOL TABEL POINTER
	MOVEM 1,116
	MOVE 1,[JRST BGN1]
	MOVEM 1,120		;SAVE ENTRY POINT IN CASE OF
;				RESTART--(WAS CLOBBERED BY DUMP)
	MOVEI 1,400000	;KILL THYSELF
	HALTF
	JRST .-1
;
;
;
PROCDP:	;PROCESS ENTIRE DUMP AREA
	SETZM 2
	PUSHJ P,IOCTL	;READ DUMP CONTROL AREA
	SKIPN CTLBUF	;PRIMARY AREA NOT EMPTY?
	JRST PRO1	;YES
;
	MOVEI 5,CTLBUF	;SET UP POINTERS FOR PRIMARY AREA
	MOVE 1,[DSKDML+1]   ;DISK ADR OF AREA
	PUSHJ P,DMPARA	;PROCESS THE AREA
	HRLI 2,10	;ELIMINATE FIRST DUMP NOW THAT IT IS PROCESSED
	PUSHJ P,IOCTL
;
PRO1:
	SKIPN CTLBUF+DSKARS  ;SECONDARY AREA EMPTY
	JRST PRO2	;YES
	MOVEI 5,CTLBUF+DSKARS ;SET UP POINTERS
	MOVE 1,[DSKDML+1+DSKDNP] ;DISK ADDRESS FOR SEC. AREA
	PUSHJ P,DMPARA
	HRLI 2,10	;ELIMINATE SECOND DUMP
	PUSHJ P,IOCTL
;
PRO2:
	POPJ P,
;
;
;
;
IOCTL:		;READ CONTROL AREA
	MOVE 1,[400000,,DSKDML]  ;DISK ADDRESS
	HRRI 2,CRABSZ		;SIZE
	MOVEI 3,CTLBUF		;ADDRESS
	DSKOP
	SKIPN 1
	POPJ P,
	HRROI 2,[ASCIZ /
IO ERROR IN CONTROL PAGE--CONTINUING/]
	PSOUT
	POPJ P,
;
;
DMPARA:		;PROCESS A GIVEN DUMP AREA
	HRLI 1,400000	;LINEAR ADDRESS
	PUSH P,5
	PUSH P,1
;
;	READ FIRST PAGE
	MOVEI 2,1000-20
	MOVEI 3,20
	DSKOP
	SKIPE 1
	AOS ERFLG
;
;
	MOVE 1,CRADAT	;FIGURE OUT WHAT PAGE IT IS IN AND READ IT
	ANDI 1,777000	;WANT PAGE IT IS 	
	MOVE 3,1
	JUMPE 3,DMPAR1
	LSH 1,-^D9
	ADD 1,0(P)	;COMPUT LOGICAL ADDRESS
	MOVEI 2,1000	;SIZE
	DSKOP
	SKIPE 1 	;ERROR
	AOS ERFLG	;YES
;
DMPAR1:	MOVE 14,CRADAT	;RESTORE POINTER TO DUMP PARAMETERS
;
;
;	FORM NAME OF FILE--EXAMPLE OF HOW IT LOOKS*
;	H45001.3APR71.140501
;	THIS WAS A BUGHLT AT 45001 ON APRIL 3,1971 AT 14:05:01
;
	SETOM 2		;TIME AND DATE SET AT TIME OF CRASH?
	SKIPGE TADSEC(14)	
	JRST ARA2	;NO--USE CURRENT TIME AND DATE
	MOVE 1,TODCLK(14)
	IDIVI 1,^D1000	;CONVERT TO SECONDS
	ADDI 1,^D10
	ADD 1,TADSEC(14)	;INTERNAL FORM TIME AND DATE
	IDIVI 1,^D24*^D3600  ;GET DAYS
	ADD 1,TADDAY(14)
	HRLI 2,0(1)
	CAIE 5,CTLBUF		;INSURE UNIQUE NAMES BY BUMPING TIME
	ADDI 2,^D60		;SECONDARY AREA GETS LATER TIME
ARA2:
	MOVE 3,[40,,0]	;GET DTAE OF FORM 3 APR 70 1306:03
	HRROI 1,DATSTR
	ODTIM
;
	MOVE 1,[POINT 7,FILSTR-1,34]
	MOVE 3,[POINT 7,DSTR-1,34]  ;PUT DIR NAME IN FILE NAME STRING
ARA21:	ILDB 2,3
	JUMPE 2,ARA20	;DONE WITH NAME
	IDPB 2,1
	JRST ARA21
ARA20:			;NOW FORM FILE NAME ITSELF
	MOVEI 2,"C"	;DETERMINE BUGCHK OR BUGHLT
	MOVE 3,[POINT 3,6(14),17]
	SKIPE 6(14)
	JRST ARA3	;IT IS A BUGCHK
	MOVEI 2,"H"	;GET H FOR BUGHLT
	ADDI 3,1
ARA3:
	IDPB 2,1	;FIRST CHAR OF FILE NAME
	MOVEI 6,6
ARA4:
;	CONVERT CRASH LOCATION TO ASCII
	ILDB 2,3	;GET BYTE OF CRASH LOCATION
	ADDI 2,"0"	;CONVERT TO ASCII
	IDPB 2,1	;SAVE IN FILE NAME
	SOJG 6,ARA4	;GET 6 CHARACTERS
	MOVEI 2,"."	;FILE NAME CREATED
	IDPB 2,1
;
	MOVE 3,[POINT 7,DATSTR-1,35]	;NOW CREATE EXTENSION
	MOVEI 6,9	;USE DAY,MONTH,YEAR
ARA5:
	ILDB 2,3	;GET DTAE CHARACTER
	CAIE 2," "	;ELIMINATE BLANKS
	CAIN 2,"-"	;ELIMINATE ---MUST KEEP FILE NAME SMALL
	SKIPA
	IDPB 2,1
;
	SOJG 6,ARA5
	MOVEI 2,";"	;PUT IN ; FOR VERSION
	IDPB 2,1
	MOVEI 6,4
	MOVE 3,[POINT 7,DATSTR+1,35]
ARA6:
	ILDB 2,3
	CAIE 2,":"	;IGNORE THE :
	CAIN 2," "
	SKIPA
	IDPB 2,1
	SOJG 6,ARA6
	MOVEI 2,0	;INDICATE END OF NAME WITH ZERO
	IDPB 2,1
;
	;FILE NAME ALL DONE--NOW GET JFN
;
;
	MOVE 1,[401001,,0]  ;SHORT JFN
	HRROI 2,FILSTR	;POINTER TO NAME
	GTJFN
	JRST ARA1	;FAILURE ON GTJFN
	PUSH P,1	;SAVE JFN NUMBER
;
;	START READING IN PAGES FROM DUMP
;
	SETZM ERFLG	;INIT I/O ERROR FLAG
	MOVE 1,-1(P)	;DISK ADR OF FIRST PAGE
	MOVE 5,-2(P)	;POINTER TO BITS
	MOVEI 2,1000	;SIZE OF PAGE
	MOVEI 3,0	;CORE ADDRESS
;
;	MORE INITIALIZATION
	MOVEI 7,DSKARS	;NUMBER OF WORDS OF BITS
;
ARA7:
	MOVEI 6,^D32	;NO. OF BITS TO CHECK
	MOVE 8,0(5)	;GET WORD OF BITS
	SETZM 0(5)	;INDICATE NO DUMP THERE ANYMORE
	JUMPE 3,ARA8	;DONT REREAD FIRST PAGE
ARA9:
	JUMPGE 8,ARA8	;NO PAGE DUMPED HERE
;
;	DONT READ OVER TOP OF THIS PROGRAM (SHOULD ONLY BE POSSIBLE
;	IF THERE IS GARBAGE IN DUMP FILE9
	CAIL 3,BIOMRA_^D9
	CAML 3,[<BIOMRA+40>_^D9]
	SKIPA
	JRST ARA8
	CAIL 3,770000	;TEMP--LEAVE DDT IN
	JRST ARA8
;
	MOVE 16,1
	DSKOP		;DO READ
	SKIPE 1		;ERRORS
	AOS ERFLG	;THERE WERE I/O ERRORS
	MOVE 1,16
ARA8:			;GO TO NEXT PAGE
	ADDI 3,1000
	ADDI 1,NSECPG	;NEXT DISK ADDRESS
	LSH 8,1		;TRY NEXT PAGE
	SOJG 6,ARA9
	ADDI 5,1	;GO TO NEXT BIT WORD
	SOJG 7,ARA7
;	DONE READING IN DUMP
;
;	PUT INTO SAVE FILE
	MOVSI 1,400000	;THIS FORK
	HRR 1,0(P)	;JFN #
	MOVE 2,[-770,,560000]	;SAVE ENTIRE PROG
	SETZM 3
	SSAVE
;
;
;	TYPE OUT SUCCESS FAILURE MESSAGES
	HRROI 2,[ASCIZ /
IO ERRORS READING  DUMP AREA /]
	SKIPE ERFLG
	PUSHJ P,TMSGQ	;THERE WERE ERRORS
;
	HRROI 2,[ASCIZ /
FILE /]
	PUSHJ P,TMSGQ
	HRROI 2,FILSTR	;TYPE OUT FILE NAME
	PUSHJ P,TMSGQ
	HRROI 2,[ASCIZ / SAVED FOR YOUR ENJOYMENT /]
	PUSHJ P,TMSGQ
	SUB P,[3,,3]
	POPJ P,
;
;
TMSGQ:		;ROUTINE TO OUTPUT STRING
	MOVEI 1,101	;CONTROLLING TTY
	MOVEI 3,0
	SOUT
	POPJ P,
;
;
ARA1:
	HRROI 2,[ASCIZ /
ERRORS SETTING UP DUMP FILE /]
	PUSHJ P,TMSGQ
ARA1A:	HRROI 2,[ASCIZ /
NO DUMP FILE CREATED/]
	PUSHJ P,TMSGQ
	SUB P,[2,,2]
	POPJ P,
;
;
;
CLRAR:		;CLEAR CONTROL PAGE AND WRITE IT OUT
	SETZM CTLBUF
	MOVE 1,[XWD CTLBUF,CTLBUF+1]
	BLT 1,CTLBUF+CRABSZ-1
;
;	WRITE OUT PAGE
	HRLZI 2,10
	PUSHJ P,IOCTL
	POPJ P,0
;
DMP1:		;SEE IF AREA IS TO BE INITIALIZED
	HRROI 2,[ASCIZ /
PROCESS EXISTING DUMPS?/]
	PUSHJ P,TMSGQ
	PBIN
	CAIN 1,"Y"
	JRST STRT1

DMP2:
	HRROI 2,[ASCIZ /
INIT DUMP AREA?/]
	PUSHJ P,TMSGQ
	PBIN
	CAIE 1,"Y"
	JRST DMP3
	PUSHJ P,CLRAR	;CLEAR THE AREA
	HRROI 2,[ASCIZ /
DONE/]
	PUSHJ P,TMSGQ
	JRST XIT

DMP3:
	HRROI 2,[ASCIZ /
FORGET IT/]
	PUSHJ P,TMSGQ
	JRST XIT


PROSZ=.-AAA
	LIT

	DEPHASE
	END START
