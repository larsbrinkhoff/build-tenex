;<N-EXEC>X5CMD.MAC;2    11-OCT-73 12:57:50	EDIT BY PLUMMER
; ADD RFPOS AT NETL55+
;<EXEC>X5CMD.MAC;5     2-OCT-73 13:14:17	EDIT BY PLUMMER

; 1.51
;<N-EXEC>X5CMD.MAC;3    23-AUG-73 12:53:51	EDIT BY PLUMMER
; REPAIR BUGS IN GTAD TIME HANDLER
;<N-EXEC>X5CMD.MAC;2    30-MAY-73 11:41:13	EDIT BY PLUMMER
; ADD GTAD "TIME OF LAST UPDATE" PROVISIONS
;<N-EXEC>X5CMD.MAC;15    27-MAR-73 13:49:32	EDIT BY PLUMMER
;<N-EXEC>X5CMD.MAC;14    27-MAR-73 12:10:56	EDIT BY PLUMMER
; NETLOAD: BE SURE WORD-1 IS NON-ZERO OF INFO FILE
;<EXEC>X5CMD.MAC;2    26-FEB-73 17:29:47	EDIT BY PLUMMER

; 1.50
;<N-EXEC>X5CMD.MAC;12    26-FEB-73 11:11:08	EDIT BY PLUMMER
;<N-EXEC>X5CMD.MAC;11    26-FEB-73 11:04:12	EDIT BY PLUMMER
;<N-EXEC>X5CMD.MAC;9    22-FEB-73 18:11:39	EDIT BY PLUMMER
;<N-EXEC>X5CMD.MAC;8    22-FEB-73 13:36:26	EDIT BY PLUMMER
; MAKE "NETLOAD" SUSPICIOUS OF FILE FORMAT
;<N-EXEC>X5CMD.MAC;6    14-FEB-73 11:49:07	EDIT BY PLUMMER
; FORMAT IMPROVEMENTS AND ADDITIONAL COMMENTS IN "NETLOAD"
;<N-EXEC>X5CMD.MAC;5    13-FEB-73 17:20:34	EDIT BY PLUMMER
; ALL NUMBER OF USERS TO "NETLOAD" TYPE OUT
;<N-EXEC>X5CMD.MAC;4     8-FEB-73 17:48:35	EDIT BY PLUMMER
;<N-EXEC>X5CMD.MAC;3     8-FEB-73 14:16:59	EDIT BY PLUMMER
;<N-EXEC>X5CMD.MAC;2     7-FEB-73 14:56:01	EDIT BY PLUMMER
;<PLUMMER>X5CMD.MAC;8     7-FEB-73 11:13:39	EDIT BY PLUMMER


; PDP-10 TENEX EXECUTIVE

;CONTENTS:
;		NETLOAD


;THE ROUTINES IN THIS FILE ARE ALL PART OF THE "DISTRIBUTED
; OPERATING SYSTEM" FUNCTIONS.  A NORMAL EXEC MAY BE ASSEMBLED
; BY OMITTING THIS FILE.
; THIS WILL HAPPEN IF DST10X IS 0.



;NETLOAD

;PRINTS THE 5 MIN. LOAD AVERAGES FROM
; ALL COOPERATING TENEX SITES.  THIS INFORMATION IS KEPT IN
; THE FILE <SYSTEM>RSYSTAT.;1   PAGE 0.

;WORD-0 OF THIS PAGE IS THE (TIME FORMAT) TIME OF LAST UPDATE
;WORD-1 IS N,,PTR  WHERE  N  IS THE LENGTH OF THE BLOCK ASSOCIATED
; WITH EACH SITE, AND PTR IS THE RELATIVE ADDRESS OF THE FIRST
; SITE BLOCK.
;WORD-5 IF PRESENT, IS GTAD FORMAT TIME OF LAST UPDATE

;EACH SITE BLOCK HAS THE FOLLOWING THINGS OF INTEREST IN IT:
;WORD-0:	SITE NUMBER
;WORD-4:	-1 IF DATA IS GOOD FOR THIS SITE
;WORD-7:	1 MIN. LOAD AV.
;WORD-10:	5 MIN. LOAD AV.
;WORD-11:	15 MIN. LOAD AV.
;WORD-12:	NUMBER OF USERS


.NETLO:	HRROI 2,[ASCIZ /<SYSTEM>RSYSTAT.;1/]
	CALL TRYGTJ		;ASSIGN AND STACK JFN
NETLO0:	 ERROR <NETWORK LOAD STATISTICS NOT AVAILABLE>
	MOVE 2,[44B5+1B19+1B25]	;READ, THAWED
	OPENF
	 JRST NETLO0		;GO TYPE ERROR
	HRLZS 1			;FROM FILE PAGE 0
	MOVE 2,[400000,,<NSBUF/1000>]	;TO ADDRESS SPACE
	MOVSI 3,(1B2)		;FOR READING
	PMAP

;PICK ONE OF THE TIME ENTRIES TO SEE IF UPDATED RECENTLY ENOUGH

NETLO1:	HRRZ 1,NSBUF+1		;GET PTR
	CAIG 1,5		;GTAD FORMAT EXISTS?
	 JRST NETLO3		;NO, USE TIME

;USE "GTAD" FORMAT TIME OF LAST UPDATE

NETLO2:	GTAD			;NOW
	SUB 1,NSBUF+5		;MINUS LAST UPDATE
	TRNE 1,1B18		;SECONDS WRAPPED AROUND?
	ADD 1,[-1,,^D<24*60*60>];YES, BORROW A DAY
	SKIPE NSBUF+1		;PARANOIA
	CAIL 1,^D<5*60>		;UPDATED WITHIN LAST 5 MINUTES?
	 JRST NETL39		;NO, LOSE
	JRST NETLO4

;USE "TIME" FORMAT WORD

NETLO3:	TIME
	IMULI 2,^D<5*60>
	SUB 1,2			;5 MINUTES BEFORE NOW
	CAMG 1,NSBUF+0		;BETTER BE PRIOR TO MOST RECENT UPDATE
	SKIPN NSBUF+1		;TEST THE N,,PTR WORD
NETL39:	 JRST [	SETOM 1
		MOVE 2,[400000,,<NSBUF/1000>]
		PMAP		;GET RID OF PAGE
		UERR [ASCIZ /SERVER DEAD/]]

NETLO4:	TYPE <  SITE		 LOAD  USERS
>
	HRRZ D,NSBUF+1		;PTR TO FIRST SITE BLOCK

NETLO5:	MOVE 1,COJFN
	SKIPN NSBUF(D)		;END OF ALL SITES?
	 JRST NETLO7		;YES, DONE.
	MOVE 3,NSBUF+4(D)
	CAME 3,[-1]		;DO WE HAVE GOOD DATA FOR THIS ONE?
	 JRST NETLO6		;NO SKIP IT
	MOVEI 2," "
	BOUT
	MOVE 2,NSBUF+0(D)	;GET BACK SITE NUMBER
	MOVEI 3,^D10
	CVHST			;PRINT HOST NAME, OR ...
	 NOUT			;NUMBER IF THAT FAILS
	  JFCL

;BE APPROPRIATELY SUSPICIOUS OF THE FILE FORMAT
NETL55:	PRINT TAB
	RFPOS
	MOVEI 2,0(2)
	CAIG 2,^D10		;WAS FIRST TAB ENOUGH?
	PRINT TAB		;NO
	SKIPGE 2,NSBUF+10(D)	;THAT SITE'S 5 MIN. LOAD AV
	JRST NETL57		;MUST BE POSITIVE
	MOVE 3,2
	TLZN 3,(1B1)
	JRST NETL56		;LOAD LESS THAN 0.5 -- OK
	TLNN 3,370000		;EXPONENT TOO BIG?
	TLNN 3,400		;NOT NORMALIZED FLOATING NUMBER?
	 JRST NETL57		;YES.
NETL56:	MOVE 3,[1B4+1B6+2B23+2B29] ;FORCE, WITH ".", 2 BEFORE AND AFTER
	FLOUT
NETL57:	 TYPE <  ?  >
	TYPE <  >
	MOVE 3,[1B2+3B17+12]	;RIGHT JUST, 3 COLS, DECIMAL
	SKIPL 2,NSBUF+12(D)	;NUMBER OF USERS ON THAT SYSTEM
	NOUT
	 TYPE <  ?>
	PRINT EOL


NETLO6:	HLRZ 1,NSBUF+1		;SITE BLOCK LENGTH
	ADDI D,0(1)		;BUMP TO NEXT BLOCK
	JRST NETLO5		;AND DO IT

NETLO7:	SETOM 1
	MOVE 2,[400000,,<NSBUF/1000>]
	PMAP			;FLUSH PAGE
	JRST RLJFNS		;GO RELEASE THE JFN

	END EXEC

D FOR THIS SITE
;WORD-7:	1 MIN. LOAD AV.
;WORD-10:	5 MIN. LOAD AV.
;WORD-11:	15 MIN. LOAD AV.
;WORD-12:	NUMBER OF USERS


.NETLO:	HRROI 2,[ASCIZ /<SYSTEM>RSYSTAT.;1/]
	CALL TRYGTJ		;ASSIGN AND STACK JFN
NETLO0:	 ERROR <NETWORK LOAD STATISTICS NOT AVAILABLE>
	MOVE 2,[44B5+1B19+1B25]	;READ, THAWED
	OPENF
	 JRST NETLO0		;GO TYPE ERROR
	HRLZS 1			;FROM FILE PAGE 0
	MOVE 2,[400000,,<NSBUF/1000>]	;TO ADDRESS SPACE