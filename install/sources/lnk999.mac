TITLE	LNK999 - EMERGENCY SEGMENT FOR LINK-10
SUBTTL	D.M.NIXON/DMN		24-JUN-74


;***COPYRIGHT 1973, 1974  DIGITAL EQUIPMENT CORP., MAYNARD, MASS.***

ENTRY	LNK999
SEARCH	LNKPAR,LNKLOW,C,SCNMAC
EXTERN	LNKCOR,LNKLOG



CUSTVR==0		;CUSTOMER VERSION
DECVER==2		;DEC VERSION
DECMVR==0		;DEC MINOR VERSION
DECEVR==135		;DEC EDIT VERSION



VERSION

SALL
SEGMENT

SUBTTL	REVISION HISTORY

;START OF VERSION 1A
;64	REDUCE CORE SIZE IF ALL OF CORE IS TAKEN
;77	RESET VERBOSITY IF LNKERR GETSEG FAILS

;START OF VERSION 2
SUBTTL	EMERGENCY GETSEG

LNK999:
IFE FTSINGLE,<			;SO WE CAN ASSEMBLE WITH ONE CMD FILE
	JFCL	.+1
	MOVE	P,SAVEAC+P	;RESTORE PUSHDOWN POINTER
	MOVE	T1,['LNKERR']	;SEE WHICH SEGMENT WAS WANTED
	CAME	T1,SEGBLK+1	;AS LNKERR CAN CAUSE A LOOP
	JRST	.+4		;NO
	MOVEI	T1,V%M		;ONLY THIS MUCH
	CAMGE	T1,VERLVL	;BUT NO MORE
	MOVEM	T1,VERLVL	;AS WE WOULD LOOP
	.ERR.	(MS,,V%L,L%I,S%I,999,<Emergency GETSEG done>)
	HRRZ	T1,SEGBLK+2	;GET ERROR CODE
	CAIN	T1,ERNEC%	;NOT ENUF CORE?
	JRST	REDCOR		;YES, REDUCE SIZE
LNKDIE:	PUSH	P,[0]		;GETSEG ON CHAN 0
	.ERR.	(LRE,,V%L,L%F,S%F,GSE)

SUBTTL	REDUCE CORE

REDCOR:	MOVE	T1,GS.AB	;TOP OF ACTIVE CORE
	IOR	T1,.PGSIZ	;IN PAGES
	SUB	T1,.JBREL	;FIND WHATS FREE
	MOVM	T1,T1		;IN A POSITIVE WAY
	CAIL	T1,8*2000	;THIS SHOULD BE ENOUGH
	JRST	[MOVE	T1,.JBREL	;GET CURRENT TOP
		SUBI	T1,8*2000	;REMOVE ENOUGH
		JRST	CORUUO]		;
	PUSHJ	P,DY.GBC##	;RETURN ALL SPACE WE CAN
	PUSHJ	P,FX.GBC##
	MOVE	T1,GS.AB	;SAVE LENGTH OF GS AREA IN USE
	SUB	T1,GS.LB	;SINCE IT WILL MOVE AND GROW
	PUSH	P,T1		;DURING EXPANSION
	PUSH	P,MAXCOR
	MOVE	T1,.JBREL	;DON'T ALLOW CORE TO EXPAND
	MOVEM	T1,MAXCOR	;INCASE NO ALL TAKEN YET
	MOVEI	P1,GS.IX
	MOVEI	P2,8*2000
	PUSHJ	P,LNKCOR##	;TRY TO GET ALL WE NEED
	  CAIA			;TRY AGAIN
	JRST	REDCR2		;GOT IT
	PUSHJ	P,FR.CNT##	;SEE WHATS LEFT
	ANDCM	T1,.PGSIZ	;IN PAGES OR K
	JUMPE	T1,LNKDIE	;NO CORE AVAILABLE
	MOVE	P2,T1		;TRY FOR THIS
	PUSHJ	P,LNKCOR##
	  JRST	NO.COR##	;CAN NOT FAIL
REDCR2:	POP	P,MAXCOR	;RESTORE FORMER LIMIT
	POP	P,T1		;GET LENGTH OF GS AREA BACK
	ADD	T1,GS.LB	;FIND END
	MOVEM	T1,GS.AB
CORUUO:	CORE	T1,		;CUT BACK
	  JFCL			;NEVER FAILS
	MOVE	T1,.JBREL	;END OF MEMORY
	MOVEM	T1,GS.UB
	PUSH	P,[0]		;GETSEG ON CHAN 0
	.ERR.	(LRE,,V%L,L%W,S%W,GSE)
	MOVE	T1,SEGBLK+1	;GET NAME WE WANT
	JRST	@NXTSEG		;GET IT



>;END OF IFE FTSINGLE
END	LNK999

END
