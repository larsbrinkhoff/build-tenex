;<SOURCES>DLUSER.MAC;7    15-APR-75 02:10:38    EDIT BY CLEMENTS
; FIX FOR BKJFN NOT WORKING ON DTA
; PUT USER NUMBER IN 100 LIKE CHECKDISK
;<SOURCES>DLUSER.MAC;6    27-MAR-75 17:55:38    EDIT BY CLEMENTS
; ADDED ABILITY TO DUMP AND RESTORE HASHED PASSWORDS, RETAINING OLD
;  ASCII STYLE TOO.
;<SOURCES>DLUSER.MAC;5    30-AUG-74 19:03:37    EDIT BY CLEMENTS
; ADD NEW FORMAT, SHORTENING DATA. ADD FLAG PER USER FOR
; EXISTENCE OF MAILBOX OR NOT.
;<SOURCES>DLUSER.MAC;3    11-APR-74 10:48:49	EDIT BY PLUMMER
; ALLOW FOR CRDIR SKIPPING

;4 NOV 71, 1319:
;DUMP/LOAD USER INFORMATION - D. MURPHY

	TITLE DLUSER

	SEARCH	STENEX		;ADDED  06MAR74  SRS

F=0
P=17

;FLAGS IN RH F

IOF==1		;1 MEANS DUMPING
FIRSTF==2	;1 MEANS AFTER FIRST DATUM OUTPUT
MBXF==4		;1 MEANS THIS DIR HAS A MAILBOX
CMAF==10	;1 MEANS RDSTR STOPS ON COMMA
NINF==20	;1 MEANS DONIN SAW AT LEAST ONE DIGIT
HPWF==40	;1 MEANS HASHED PASSWORD SEEN, ELSE ASCIZ.
BAKUPF==100	;REPLACES BKJFN WHICH DOESN'T FLY ON DECTAPE

LIGHTS==100	;PUT CURRENT USER NUMBER IN 100 LIKE CHECKDISK

OPDEF CALL [PUSHJ P,0]
OPDEF RET [POPJ P,0]

MAXUSR=2000

	MLON

	INTERN START,LODUSR,DMPUSR

EVEC:	JRST START		;MAIN START
	JRST LODUSR		;CONTINUE LOADING IN CASE CRDIR FAILS

DEFINE LS (NM,SZ)
<NM:	BLOCK SZ
>

NPDL==20

LS JFN,1
LS EBUF,20
EEBUF=.-1
LS NAMBUF,11
LS PASBUF,11
LS STRTMP,20
LS PDL,NPDL

START:	MOVE P,[IOWD NPDL,PDL]
	MOVEI F,0		;START WITH ALL FLAGS OFF
	RESET
USR1:	HRROI 1,[ASCIZ /
DUMP OR LOAD? /]
	PSOUT
	PBIN
	CAIN 1,"L"
	JRST USR2		;LOAD. LEAVE DUMP FLAG OFF
	CAIE 1,"D"
	JRST USR1		;NOTA, TRY AGAIN
	TRO F,IOF		;DUMP. SET OUTPUT FLAG
USR2:	HRROI 1,[ASCIZ /
FILE: /]
	PSOUT
	MOVE 2,[XWD 100,101]
	MOVSI 1,(1B2+1B3+1B4+3B17)	;FLAGS FOR INPUT JFN
	TRNE F,IOF
	MOVSI 1,(1B0+1B3+1B4+3B17)	;FLAGS FOR OUTPUT JFN
	GTJFN
	 JRST USR2
	HRRZM 1,JFN
	MOVE 1,JFN
	MOVE 2,[7B5+1B19]	;7-BIT, READ
	TRNE F,IOF		;SKIP IF LOADING
	HRRI 2,1B20		;MAKE THAT WRITE
	OPENF
	 JRST [MOVE 1,JFN
		RLJFN
		  JFCL
		JRST USR2]
	HRROI 1,CRLFM
	PSOUT
	TRNE F,IOF
	JRST DMPUSR		;GO DO THE THING
	JRST LODUSR

;DUMP USERS

DMPUSR:	MOVEI 10,1
	MOVE 1,JFN
	HRROI 2,FORMAT
	SETZ 3,
	SOUT
DMPUS3:	TRZ F,HPWF		;ASSUME ASCII FORMAT
	HRROI 1,NAMBUF
	MOVEI 2,0(10)
	DIRST
	JRST DMPUS1		;NUMBER NOT IN USE
	MOVEM 10,LIGHTS		;WATCH ON CONSOLE
	MOVEI 1,0(10)
	MOVEI 2,EBUF
	HRROI 3,PASBUF
	GTDIR			;GET PASSWORD AND DIRECTORY PARAMS
	LDB 1,[POINT 6,EBUF+1,11]	;ASCII OR HASHED PASWD?
	CAIN 1,44		;WORD LENGTH MEANS HASHED
	TRO F,HPWF		; ..
	MOVE 1,JFN
	MOVEI 2,"!"
	BOUT			;MARK START OF NAME
	HRROI 2,NAMBUF
	SETZ 3,
	SOUT			;OUTPUT NAME
	TRNN F,HPWF		;HASHED OR OLD ASCII?
	JRST DMPOPW		;OLD STYLE
DMPHPW:	PUSHJ P,CRLF		;END OF LINE
	MOVEI 2,"$"		;FLAG FOR HASHED PASSWORD
	BOUT			;OUTPUT IT
	MOVE 2,PASBUF+0		;FIRST WORD OF HASH
	MOVE 3,[1B0+10]		;OUTPUT NUMBER AS MAGNITUDE
	NOUT
	 0
	MOVEI 2,","		;SEPARATE THE TWO NUMBERS
	BOUT
	MOVE 2,PASBUF+1		;SECOND NUMBER OF HASH
	NOUT
	 0
	JRST DMPPAR		;GO DO THE PARAMS

DMPOPW:	PUSHJ P,CRLFSP
	HRROI 2,PASBUF
	SOUT
DMPPAR:	PUSHJ P,CRLF
	MOVSI 7,-15+2		;15 ENTRIES LESS NAME AND PASSWD
	TRZ F,FIRSTF!MBXF	;INIT COUNTER AND ASSUME NO MAILBOX
	; ..

DMPUS2:	SKIPN 5,EBUF+2(7)	;NON-ZERO DATUM?
	JRST DMPUN1		;NOPE (AT LEAST 1 MUST BE, THOUGH)
	MOVEI 2,","		;PUT OUT A COMMA ALL BUT FIRST DATUM
	TROE F,FIRSTF		; ..
	BOUT
	MOVEI 2,"#"		;PUT OUT "#N="
	BOUT
	MOVEI 2,2(7)
	MOVEI 3,10
	NOUT
	 0
	MOVEI 2,"="
	BOUT
	MOVSI 6,-NNUMS		;SEE IF NUMBER HAS A SHORTHAND
	CAMN 5,NUMTB(6)
	JRST NUMLTR		;YES
	AOBJN 6,.-2
	MOVE 2,EBUF+2(7)	;NO. OUTPUT THE NUMBER.
	MOVE 3,[1B0+10]
	NOUT
	 0
DMPUN1:	AOBJN 7,DMPUS2
	MOVEI 1,MBXJFN
	HRROI 2,[ASCIZ /MESSAGE.TXT;1/]	;SEE IF MAILBOX EXISTS
	GTJFN
	  JRST NOMBX		;NOPE
	MOVE 2,[1,,1]		;YES, SEE IF IT'S PERMANENT
	MOVEI 3,4
	GTFDB
	TLNE 4,(1B1)		;PERM BIT
	TRO F,MBXF		;YES. WANT A MAILBOX
	RLJFN
	  JFCL
NOMBX:	MOVE 1,JFN
	HRROI 2,[ASCIZ /,NOMAIL/]
	MOVEI 3,0
	TRNN F,MBXF
	SOUT			;NO MAILBOX
	PUSHJ P,CRLF
DMPUS1:	CAIGE 10,MAXUSR-1
	AOJA 10,DMPUS3
	JRST DONE

NUMLTR:	MOVEI 2,"A"(6)
	BOUT
	JRST DMPUN1

MBXJFN:	101000,,1
	377777,,377777
	0
	440700,,NAMBUF
	0
	0
	0
	0
	0

DONE:	MOVE 1,JFN
	CLOSF
	JFCL
	HRROI 1,[ASCIZ /
DONE.
/]
	PSOUT
	HALTF

FORMAT:	ASCIZ /

DIRECTORY NAME
 PASSWORD
 DISK LIMIT
 CAPABILITIES
 FILES ONLY, ALPHA ACCTS, REPEAT LMSG
 SPECIAL RESOURCE INFO
 DIRECTORY NUMBER
 DEFAULT FILE PROTECTION
 DIRECTORY PROTECTION
 DEFAULT RETENTION SPECIFICATION
 LAST LOGIN
 USER GROUP WORD
 DIRECTORY GROUP WORD

/
;LOAD USERS

LODUSR:	TRZ F,HPWF		;ASSUME ASCII PASSWORD
	MOVE 1,JFN
LODUS1:	PUSHJ P,DOBIN
	JUMPE 2,LODEND		;NULL, PROBABLY EOF
	CAIE 2,"!"		;SCAN FOR NAME MARKER
	JRST LODUS1
	TRO F,MBXF		;ASSUME WILL MAKE MAILBOX FILE
	MOVE 5,[POINT 7,NAMBUF,-1]
	MOVEM 5,EBUF
	CALL RDSTR		;READ STRING TO CR
	PUSHJ P,DOBIN		;SEE WHAT FIRST CHAR IS ON PSWD LINE
	CAIE 2," "		;SPACE IS OLD STYLE ASCII
	CAIN 2,"$"		;DOLLARS IS NEW HASHED BINARY
	SKIPA			;ONE OF THOSE TWO.
	JRST LODFER		;NOT RIGHT FORMAT
	CAIN 2,"$"		;NEW?
	TROA F,HPWF		;YES. REMEMBER THAT.
	JRST LODOPW		;NO, OLD STYLE.
LODHPW:	PUSHJ P,DONIN		;GET FIRST WORD OF HASH
	  JRST LODFER		;NO GOOD
	MOVEM 2,PASBUF		;OK. SAVE NUMBER
	CAIE 3,","		;SEPARATOR
	JRST LODFER
	PUSHJ P,DONIN		;GET SECOND NUMBER
	  JRST LODFER
	CAIL 3,40		;SHOULD BE EOL
	JRST LODFER
	MOVEM 2,PASBUF+1	;SECOND NUMBER OF HASH
	MOVE 2,3		;COPY OVER THE EOL CHAR
	TRO F,BAKUPF		;REMEMBER TO SEE IT
	MOVE 3,[444400,,PASBUF]	;BINARY POINTER TO PASSWORD
	MOVEM 3,EBUF+1		;ARG TO CRDIR
	JRST LODPAR		;GO LOAD THE USER DESCRIPTOR PARAMS

LODOPW:	MOVE 5,[POINT 7,PASBUF,-1]
	MOVEM 5,EBUF+1
	CALL RDSTR		;READ PASSWORD
LODPAR:	SETZM EBUF+2		;CLEAR ALL PARAMS TO 0
	MOVE 3,[EBUF+2,,EBUF+3]
	BLT 3,EEBUF
	MOVSI 7,-15+2		;15 PARAMS LESS NAME AND PASSWD
	TRZN F,BAKUPF		;DON'T BIN HERE, HAVE BRK FROM NIN
LODFMQ:	PUSHJ P,DOBIN
	CAIN 2," "		;NEXT LINE STARTS WITH SPACE?
	JRST LODUSO		;YES. OLD FORMAT.
	CAIN 2,"#"		;STARTS WITH NUMBER SIGN?
	JRST LODUSN		;YES, NEW FORMAT
	JUMPE 2,LODFER		;IN CASE EOF
	CAIG 2,40		;CR, LF, ETC?
	JRST LODFMQ		;YES
LODFER:	HRROI 1,[ASCIZ /
? FORMAT ERROR ON INPUT TAPE, DURING OR AFTER USER /]
	PSOUT
	HRROI 1,NAMBUF
	PSOUT
	HRROI 1,CRLFM
	PSOUT
	HALTF
	JRST LODUSR


LODUS3:	CALL SCNSPC		;POSITION TO BEGINNING OF NUMBER
LODUSO:	MOVEI 3,10
	NIN			;READ NUMBER
	JRST [	CAIE 3,IFIXX3	;NORMAL OVERFLOW?
		JRST LODFER	;NO
		JRST .+1]	;YES, HAPPENS ON 36 BIT OCTAL NUMBER
	MOVEM 2,EBUF+2(7)
	AOBJN 7,LODUS3
LODUSC:	HRROI 1,NAMBUF
	MOVE 2,EBUF+6		;CURRENT DIRECTORY NUMBER
	MOVEM 2,LIGHTS		;TO CONSOLE LIGHTS
	MOVE 2,[XWD 367740,EBUF]
	TRNN F,MBXF		;WANT MAILBOX?
	TLO 2,(1B15)		;NO.
	CRDIR
	 JRST CRDERR
	JRST LODUSR

LODEND:	GTSTS
	TLNE 2,(1B10)		;JFN STILL ALIVE?
	TLNE 2,(1B8)
	JRST DONE		;NO, FINISHED
	JRST LODUSR		;YES, KEEP SCANNING

;HERE AFTER "#" ON PARAM LINE

LODUSN:	PUSHJ P,DONIN		;OCTAL NIN, KEEP BRK IN 3
	  JRST LODFER		;MUST BE NUMBER HERE
	CAIE 3,"="		;AND MUST BREAK WITH EQUALS SIGN
	JRST LODFER
	MOVE 12,2		;OK, SAVE INDEX TO PARAMETER
	CAIL 12,2		;MAKE SURE IN RANGE
	CAILE 12,15		; ..
	JRST LODFER		;NO.
	PUSHJ P,DONIN		;OK, GET VALUE OF THIS PARAM
	  JRST LODLTQ		;NOT NUMBER
LODUN2:	MOVEM 2,EBUF(12)	;STORE THE PARAMETER
LODUN4:	CAIN 3,","		;WAS BRK A COMMA?
	JRST LODUN1		;YES. SEE WHAT FOLLOWS
	CAIE 3,37		;SOME KIND OF EOL?
	CAIN 3,12		; ..
	JRST LODUSC		;YES. DONE. CREATE THIS USER.
	JRST LODFER		;THAT'S ALL IT CAN BE.

LODLTQ:	CAIL 3,"A"		;IN RANGE?
	CAILE 3,"A"+NNUMS-1
	JRST LODFER		;NO
	MOVE 3,NUMTB-"A"(3)	;YES, GET THE PARAMETER
	PUSHJ P,DOBIN			;AND THE BREAK
	EXCH 2,3		;INTO RIGHT AC'S
	JRST LODUN2		;GO STORE PARAM

LODUN1:	PUSHJ P,DOBIN		;HERE AFTER COMMA AFTER A PARAM
	CAIN 2,"#"		;ANOTHER NUMBER?
	JRST LODUSN		;YES
	SETZM STRTMP
	MOVE 5,[440700,,STRTMP]	;NO, SEE IF IT'S A TEXT STRING
	IDPB 2,5		;STORE FIRST CHAR
	CALL RDSTRC		;READ, QUIT ON COMMA OR EOL
	MOVE 2,STRTMP		;SEE WHAT STRING WAS
	CAME 2,[ASCII /NOMAI/]	;MAKE TABLE SEARCH IF MORE KEYWDS ADDED
	JRST LODFER		;WRONG
	TRZ F,MBXF		;RIGHT. CLEAR MAILBOX FLAG
	JRST LODUN4		;BREAK CHAR IS IN 3

DONIN:	TRZ F,NINF		;NO DIGITS YET
	MOVEI 3,0		;BUILD ANSWER HERE
DONINL:	PUSHJ P,DOBIN
	CAIL 2,60		;OCTAL DIGIT?
	CAILE 2,67		; ..
	JRST [	EXCH 2,3	;BREAK. ANSWER TO 2, BRK TO 3.
		TRNE F,NINF
		AOS 0(P)	;SKIP IF ANY DIGITS
		POPJ P,0]	; AND RETURN
	LSH 3,3			;DIGIT. ACCUMULATE ANSWER
	ADDI 3,-60(2)
	TRO F,NINF
	JRST DONINL		;ON TO BREAK

CRDERR:	HRROI 1,[ASCIZ /
? CAN'T CREATE USER /]
	PSOUT
	HRROI 1,NAMBUF
	PSOUT
	HRROI 1,[ASCIZ / AS DIRECTORY NUMBER /]
	PSOUT
	MOVEI 1,101
	MOVE 2,EBUF+6
	MOVEI 3,10
	NOUT
	  0
	HRROI 1,[ASCIZ /, CONTINUING...
/]
	PSOUT
	JRST LODUSR

RDSTRC:	TROA F,CMAF
RDSTR:	TRZ F,CMAF
	PUSHJ P,DOBIN
	MOVE 3,2		;SAVE A COPY
	CAIE 2,37		;STOP ON EITHER TYPE OF CR
	CAIN 2,12
	SETZ 2,			;APPEND NULL ON END OF STRING
	CAIN 2,","		;COMMA?
	TRNN F,CMAF		;YES. STOP ON IT?
	SKIPA			;NO
	MOVEI 2,0		;YES.
	IDPB 2,5
	JUMPN 2,RDSTR
	RET

SCNSPC:	PUSHJ P,DOBIN
	CAIE 2," "
	JRST .-2
	RET


DOBIN:	BIN
	CAIN 2,15		;SKIP CARRIAGE RETURNS
	JRST DOBIN
	POPJ P,0

CRLF:	HRROI 2,CRLFM
	MOVEI 3,0
	SOUT
	RET

CRLFSP:	PUSHJ P,CRLF
	MOVEI 2,40
	BOUT
	RET

CRLFM:	ASCIZ /
/

NUMTB:	;TABLE OF NUMBERS FOR FILE COMPRESSION. LENGTH MUST BE
	;HELD TO 26 FOR LETTERS A TO Z
	;FIRST NUMBER IS A, SECOND B, ETC. NEVER CHANGE! ONLY ADD!

	500000,,777752	;A
	500000,,777760	;B
	500000,,775200	;C
	500000,,776000	;D
	500000,,775252	;E
	500000,,776060	;F
	500000,,770000	;G
	400000,,000000	;H
	0,,400000	;I
	0,,200000	;J
	500000,,000002	;K
	500000,,0	;L
	040000,,000000	;M
NNUMS==.-NUMTB
	END START
